@echo off
setlocal enabledelayedexpansion

set VERSION=VERSION_PLACEHOLDER
set REPO=REPO_PLACEHOLDER
set IMAGE_FILE=cheeky-qemu-ubuntu-minimal-!VERSION!.qcow2
set IMAGE_URL=https://github.com/!REPO!/releases/download/!VERSION!/!IMAGE_FILE!
set QEMU_EXE=qemu-system-aarch64.exe

if not "%~1"=="" (
    set QEMU_EXE=%~1\qemu-system-aarch64.exe
)

echo Cheeky QEMU Launcher
echo Image: !IMAGE_FILE!
echo.

if not exist "!IMAGE_FILE!" (
    echo Image not found, downloading...
    powershell -NoProfile -Command "Invoke-WebRequest -Uri '!IMAGE_URL!' -OutFile '!IMAGE_FILE!'"
    if not exist "!IMAGE_FILE!" (
        echo Download failed
        pause
        exit /b 1
    )
)

echo Starting QEMU - boot takes 1-2 minutes
echo.
echo Access:
echo - Radio: http://localhost:6680
echo - Bluetooth: http://localhost:8080
echo - SSH: ssh -p 2222 root@localhost
echo.

"!QEMU_EXE!" -nographic -machine virt -cpu cortex-a72 -m 2048 -drive file="!IMAGE_FILE!",if=none,format=qcow2,id=hd -device virtio-blk-device,drive=hd -netdev user,id=net0,hostfwd=tcp::6680-:6680,hostfwd=tcp::8080-:8080,hostfwd=tcp::2222-:22 -device virtio-net-device,netdev=net0 -serial telnet:localhost:4555,server,nowait

echo.
echo QEMU exited
pause
