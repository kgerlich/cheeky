@echo off
setlocal enabledelayedexpansion
set VERSION=VERSION_PLACEHOLDER
set REPO=REPO_PLACEHOLDER
set IMAGE_FILE=cheeky-qemu-debian-!VERSION!.qcow2
set IMAGE_URL=https://github.com/!REPO!/releases/download/!VERSION!/!IMAGE_FILE!
echo Cheeky QEMU Launcher
if not exist "!IMAGE_FILE!" (
    echo Downloading image...
    powershell -NoProfile -Command "$ProgressPreference='SilentlyContinue'; Invoke-WebRequest -Uri '!IMAGE_URL!' -OutFile '!IMAGE_FILE!'"
)
if not exist "!IMAGE_FILE!" (
    echo Download failed
    pause
    exit /b 1
)
echo Starting QEMU...
set QEMU_EXE=qemu-system-aarch64.exe
set SCRIPT_DIR=%~dp0
if not "%~1"=="" (
    set QEMU_EXE=%~1\qemu-system-aarch64.exe
)
echo.
echo QEMU will boot Debian with direct kernel loading...
echo Once booted, open in your browser:
echo   Radio: http://localhost:6680
echo   Bluetooth Manager: http://localhost:8080
echo   SSH: ssh -p 2222 debian@localhost
echo.
"!QEMU_EXE!" -nographic -machine virt -cpu cortex-a72 -m 2048 -kernel "!SCRIPT_DIR!vmlinuz" -initrd "!SCRIPT_DIR!initrd.img" -append "root=/dev/vda1 console=ttyAMA0 rw" -drive file="!IMAGE_FILE!",if=none,format=qcow2,id=hd -device virtio-blk-device,drive=hd -drive file="!SCRIPT_DIR!seed.iso",if=none,format=raw,id=seed -device virtio-blk-device,drive=seed -netdev user,id=net0,hostfwd=tcp::6680-:6680,hostfwd=tcp::8080-:8080,hostfwd=tcp::2222-:22 -device virtio-net-device,netdev=net0
pause
